%import common (WS)
%import .bigquery (_AS, _COMMA, _EQ, _FROM, _SELECT, as_alias, column_expr, from_clause, ident, query_expr, query_statement, select, select_list, subquery, start)

# TODO: See ZetaSQL for a more complete definition of WS
%ignore WS

################################################################################
# Punctuation
################################################################################

_PIPE_OP : "|>"
_SEMI : ";"

################################################################################
# Keywords
################################################################################

# All keywords are case-insensitive

_DROP : "DROP"i
_EXTEND : "EXTEND"i
_RENAME : "RENAME"i
_SET : "SET"i

################################################################################
# Procedural
################################################################################

%extend query_statement : script

script : query_statement script_item

script_item : _SEMI query_statement?

################################################################################
# Pipe
################################################################################

%extend query_expr : pipe_from

pipe_from : _FROM from_clause

%extend query_expr : pipe_expr

pipe_expr : pipe_start pipe_item+

pipe_start : select | subquery | pipe_from

pipe_item : _PIPE_OP pipe_operator

pipe_operator : pipe_select
    | pipe_extend
    | pipe_set
    | pipe_drop
    | pipe_rename
    | pipe_as
#   | pipe_where
#   | pipe_limit
#   | pipe_agg
#   | pipe_order_by
#   | pipe_union
#   | pipe_intersect
#   | pipe_except
#   | pipe_join
#   | pipe_call
#   | pipe_window
#   | pipe_tablesample
#   | pipe_pivot
#   | pipe_unpivot

# SELECT

pipe_select : _SELECT select_list

# EXTEND

pipe_extend : _EXTEND extend_list

extend_list : extend_item ( _COMMA extend_item )* _COMMA?

extend_item : column_expr as_alias?

# SET

pipe_set : _SET set_list

set_list : set_item ( _COMMA set_item )* _COMMA?

set_item : ident _EQ column_expr

# DROP

pipe_drop : _DROP drop_list

drop_list : drop_item ( _COMMA drop_item )* _COMMA?

drop_item : ident

# RENAME

pipe_rename : _RENAME rename_list

rename_list : rename_item ( _COMMA rename_item )* _COMMA?

rename_item : ident as_alias

# AS

pipe_as : _AS ident